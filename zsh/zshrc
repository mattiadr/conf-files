ZSH_CONFIG="/home/mattiadr/conf-files/zsh"
alias zshrc="subl $ZSH_CONFIG/zshrc"

##########
# config #
##########
fpath=(/home/mattiadr/conf-files/zsh/zsh_completions $fpath)

# keybinds
bindkey -e
bindkey "^[[4h"   overwrite-mode                    # ins
bindkey "^[[P"    delete-char                       # del
bindkey "^[[H"    beginning-of-line                 # home
bindkey "^[[4~"   end-of-line                       # end
#bindkey "^[[5~"                                    # page up
bindkey "^[[6~"   end-of-history                    # page down
bindkey "^[[1;5A" history-beginning-search-backward # ctrl + up
bindkey "^[[1;5B" history-beginning-search-forward  # ctrl + down
bindkey "^[[1;5C" forward-word                      # ctrl + right
bindkey "^[[1;5D" backward-word                     # ctrl + left

# On an ambiguous completion, instead of listing possibilities or beeping,
# insert the first match immediately.
# setopt menu_complete

# Treat the ‘#’, ‘~’ and ‘^’ characters as part of patterns for filename
# generation, etc. (An initial unquoted ‘~’ always produces named directory
# expansion.)
setopt extended_glob

# If this is set, zsh sessions will append their history list to the history
# file, rather than replace it.
setopt append_history

# Do not enter command lines into the history list if they are duplicates of
# the previous event.
setopt hist_ignore_dups

# Remove superfluous blanks from each command line being added to the history
# list.
setopt hist_reduce_blanks

HISTFILE=~/.zsh_history
HISTSIZE=2000
SAVEHIST=2000

#With this option set, stopped jobs that are removed from the job table with
# the disown builtin command are automatically sent a CONT signal to make them
# running.
setopt auto_continue

# Beep on error in ZLE.
setopt no_beep

# completion
autoload -Uz compinit
compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' completer _expand _complete _ignored
zstyle ':completion:*' format '%d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' ignore-parents pwd
zstyle ':completion:*' matcher-list ''
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ":completion:*:commands" rehash 1

# vcs
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:git*' formats "%b %m%u%c"

precmd() {
	vcs_info
}

setopt prompt_subst

#######
# PS1 #
#######

local c_reset='%f%b'
local c_pipe='%B%F{yellow}'
local c_red='%B%F{red}'
local c_green='%B%F{green}'
local c_folder='%B%F{blue}'

if [[ $UID -eq 0 ]]; then
	local c_primary=$c_red
	local symbol=#
else
	local c_primary=$c_green
	local symbol=\$
fi

PS1="${c_pipe}[${c_reset} ${c_primary}%n${c_reset} ${c_pipe}|${c_reset} ${c_folder}%~${c_reset} ${c_pipe}]${c_primary}${symbol}${c_reset} "
RPS1='${vcs_info_msg_0_}'

PS2="${c_primary}>>${c_reset} "

###########
# aliases #
###########

source $ZSH_CONFIG/aliases
